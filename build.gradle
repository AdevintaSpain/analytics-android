buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url 'https://plugins.gradle.org/m2'
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.3.3'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.5.0'
    classpath 'com.f2prateek.javafmt:javafmt:0.1.6'
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.10'
  }
}

allprojects {
  buildscript {
    repositories {
      mavenCentral()
      jcenter()
      google()
    }
  }

  apply plugin: 'idea'
  apply plugin: 'net.ltgt.errorprone'

  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  group = GROUP
  version = VERSION_NAME

  tasks.withType(JavaCompile) {
      options.compilerArgs += [
          '-Xep:UnnecessaryDefaultInEnumSwitch:ERROR',
          '-Xep:TypeParameterUnusedInFormals:ERROR',
          '-Xep:MissingOverride:ERROR',
          '-Xep:ClassCanBeStatic:ERROR',
          '-Xep:OperatorPrecedence:ERROR',
          '-Xep:ReferenceEquality:ERROR',
          '-Xep:FloatingPointLiteralPrecision:ERROR',
          '-Xep:SimpleDateFormatConstant:ERROR',
      ]
  }
}

def isCI = "true".equals(System.getenv("CIRCLECI"))
def preDexEnabled = "true".equals(System.getProperty("preDex", "true"))

ext {
  versionCode = Integer.parseInt(VERSION_CODE)
  versionName = VERSION_NAME

  minSdkVersion = 14
  minSdkVersionWear = 21
  targetSdkVersion = 26
  compileSdkVersion = 26
  buildToolsVersion = '26.0.2'

  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7

  preDexLibraries = preDexEnabled && !isCI
}

ext.deps = [
  supportAnnotations: 'com.android.support:support-annotations:26.1.0'
]
